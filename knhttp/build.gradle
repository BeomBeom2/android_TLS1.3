plugins {
	id 'com.android.library'
	id 'maven-publish'
}

android {
	namespace 'ovh.karewan.knhttp'
	compileSdk 33

	defaultConfig {
		minSdk 21
		targetSdk 33
		versionCode 36
		versionName "3.1.0"
		consumerProguardFiles 'proguard-rules.pro'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		abortOnError false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}

	publishing {
		singleVariant("release")
	}
}

dependencies {
	implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
	implementation 'androidx.annotation:annotation:1.6.0'
	api 'com.squareup.okio:okio:3.2.0'
	api ('com.squareup.okhttp3:okhttp:4.11.0') {
		exclude group: 'com.squareup.okio', module:'okio'
		exclude group: 'com.squareup.okio', module:'okio-jvm'
	}
	implementation 'com.squareup.okhttp3:okhttp-brotli:4.11.0'
	implementation 'org.conscrypt:conscrypt-android:2.5.2'
	api 'com.alibaba:fastjson:1.1.73.android'
}

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release
				groupId = 'com.github.Karewan'
				artifactId = 'KnHttp'
				version = android.defaultConfig.versionName
			}
		}
	}
}
